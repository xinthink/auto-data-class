buildscript {
    ext.kotlin_version = '1.1.51'
    ext.gson_version = '2.8.0'

    ext.deps = [
        kotlin: [
            plugin: "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version",
            stdlib: "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version",
        ],
        auto: [
            common: 'com.google.auto:auto-common:0.8',
            // service: 'com.google.auto.service:auto-service:1.0-rc3',
        ],
        android: [
            core: 'com.google.android:android:2.1.2',
        ],
        kotlinpoet: 'com.squareup:kotlinpoet:0.5.0',
        gson: "com.google.code.gson:gson:$gson_version",

        junit: 'junit:junit:4.12',
        truth: 'com.google.truth:truth:0.27',
        compile_testing: 'com.google.testing.compile:compile-testing:0.9',
        mockito: [
            core: 'org.mockito:mockito-core:2.10.0',
            inline: 'org.mockito:mockito-inline:2.10.0',
        ],
    ]

    repositories {
        jcenter()
        maven { url "https://jitpack.io" }
        mavenCentral()
    }
    dependencies {
        classpath deps.kotlin.plugin
    }
}

plugins {
    id 'com.jfrog.bintray' version '1.7.3'
}

group 'com.fivemiles.auto'
version '0.1.0'

// load local.properties
Properties props = new Properties()
props.load new FileInputStream('local.properties')
props.each { project.ext.set(it.key, it.value) }

subprojects {
    apply plugin: 'java'
    apply plugin: 'kotlin'

    sourceCompatibility = JavaVersion.VERSION_1_7

    repositories {
        jcenter()
        maven { url "https://jitpack.io" }
        mavenCentral()
    }

    dependencies {
        compile deps.kotlin.stdlib
        testCompile deps.junit
        testCompile deps.mockito.inline
    }
}

configure(subprojects.findAll { it.name != 'example' }) {
    apply plugin: 'maven'
    apply plugin: 'maven-publish'
    apply plugin: 'com.jfrog.bintray'

    ext {
        artifactId = "auto-data-class-${project.name}"
    }

    task sourceJar(type: Jar) {
        from sourceSets.main.allJava
    }

    bintray {
        user = BINTRAY_USER
        key = BINTRAY_KEY
        publications = ['mavenJava']
        pkg {
            repo = 'maven'
            name = "auto-data-class"
            licenses = ['Apache-2.0']
            vcsUrl = 'https://github.com/xinthink/auto-data-class.git'
            version {
                name = rootProject.version
                vcsTag = "v${rootProject.version}"
            }
        }
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
                artifact sourceJar {
                    classifier 'sources'
                }
                groupId rootProject.group
                artifactId project.artifactId
                version rootProject.version
            }
        }
    }
}
